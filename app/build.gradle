apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildToolsVersion

    def STRING = 'String'
    def API_KEY = 'API_KEY'
    def BASE_API_URL = 'BASE_API_URL'
    def BASE_IMAGE_URL = 'BASE_IMAGE_URL'

    Properties props = new Properties()
    if (file('../local.properties').exists()) {
        // if it doesn't exist, the props will all be null and the builds will fail
        props.load(new FileInputStream(file('../local.properties')))
    }

    defaultConfig {
        applicationId buildConfig.packageName

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode 1
        versionName "0.1.0"

//        buildConfigField STRING, API_KEY, props['apiKey']
        buildConfigField STRING, BASE_API_URL, '"https://api/"'
        buildConfigField STRING, BASE_IMAGE_URL, '"http://image/"'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(defaultConfig.applicationId + '.jks')
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    applicationVariants.all { variant ->
        variant.getCompileConfiguration().resolutionStrategy {
            force "com.android.support:support-v7:$versions.supportLibVersion"
            force "com.android.support:support-v4:$versions.supportLibVersion"
            force "com.android.support:design:$versions.supportLibVersion"
        }

        variant.getRuntimeConfiguration().resolutionStrategy {
            force "com.android.support:design:$versions.supportLibVersion"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    // Local libs
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlinVersion"

    // Support
    implementation "com.android.support:appcompat-v7:$versions.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$versions.supportLibVersion"
    implementation "com.android.support:design:$versions.supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintLayoutVersion"

    // Network
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$versions.retrofitVersion"
    implementation "com.squareup.moshi:moshi:$versions.moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshiVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofitVersion"

    // Image
    implementation "com.github.bumptech.glide:glide:$versions.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$versions.glideVersion"

    // Dependency injection
    implementation "javax.annotation:javax.annotation-api:$versions.javaAnnotationVersion"
    implementation "com.google.dagger:dagger:$versions.daggerVersion"
    implementation "com.google.dagger:dagger-android:$versions.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$versions.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$versions.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$versions.daggerVersion"

    // LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:$versions.archVersion"
    implementation "android.arch.lifecycle:viewmodel:$versions.archVersion"

    // Paging
    implementation "android.arch.paging:runtime:$versions.archPagingVersion"

    // RX
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlinVersion"

    // RX binding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxBindingVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"

    // Test
    testImplementation "junit:junit:$versions.junitVersion"
    testImplementation "org.mockito:mockito-core:$versions.mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlinVersion"
    testImplementation "org.assertj:assertj-core:$versions.assertJVersion"
    testImplementation("android.arch.core:core-testing:$versions.archVersion", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    // Android Test
    androidTestImplementation "com.android.support.test:runner:$versions.testRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espressoVersion"
}

ktlint {
    debug = true
    verbose = true
    android = true
    outputToConsole = true
    reporters = ["PLAIN"]
}
