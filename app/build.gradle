apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildToolsVersion

    def STRING = 'String'
    def API_KEY = 'API_KEY'
    def BASE_API_URL = 'BASE_API_URL'
    def BASE_IMAGE_URL = 'BASE_IMAGE_URL'

    Properties props = new Properties()
    if (file('../local.properties').exists()) {
        // if it doesn't exist, the props will all be null and the builds will fail
        props.load(new FileInputStream(file('../local.properties')))
    }

    defaultConfig {
        applicationId buildConfig.packageName

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode 1
        versionName "0.1.0"

//        buildConfigField STRING, API_KEY, props['apiKey']
        buildConfigField STRING, BASE_API_URL, '"https://api/"'
        buildConfigField STRING, BASE_IMAGE_URL, '"http://image/"'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(defaultConfig.applicationId + '.jks')
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    applicationVariants.all { variant ->
        variant.getCompileConfiguration().resolutionStrategy {
            force "com.android.support:support-v7:$versions.supportLib"
            force "com.android.support:support-v4:$versions.supportLib"
            force "com.android.support:design:$versions.supportLib"
        }

        variant.getRuntimeConfiguration().resolutionStrategy {
            force "com.android.support:design:$versions.supportLib"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    // Local libs
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Spotify
    implementation "com.spotify.android:auth:$versions.spotify"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    // Support
    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:$versions.supportLib"
    implementation "com.android.support:recyclerview-v7:$versions.supportLib"
    implementation "com.android.support:design:$versions.supportLib"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintLayout"

    // Network
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$versions.retrofit"
    implementation "com.squareup.moshi:moshi:$versions.moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"

    // Image
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    // Dependency injection
    implementation "javax.annotation:javax.annotation-api:$versions.javaAnnotation"
    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

    // LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:$versions.arch"
    implementation "android.arch.lifecycle:viewmodel:$versions.arch"

    // Paging
    implementation "android.arch.paging:runtime:$versions.archPaging"

    // RX
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlin"

    // RX binding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxBinding"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Test
    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlin"
    testImplementation "org.assertj:assertj-core:$versions.assertJ"
    testImplementation("android.arch.core:core-testing:$versions.arch", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    // Android Test
    androidTestImplementation "com.android.support.test:runner:$versions.testRunner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
}

ktlint {
    debug = true
    verbose = true
    android = true
    outputToConsole = true
    reporters = ["PLAIN"]
}
